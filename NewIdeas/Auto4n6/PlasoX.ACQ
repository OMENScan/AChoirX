*******************************************************
* AChoirX and Plaso Timeline an AChoirX collection    *
* - Win Requires Plaso to be in &Dir\Plaso directory  *
* - Lin Requires Plaso to be in /usr/bin directory    *
* - OSX not yet implemented                           *
*   - AChoirX will Check                              *
* - Recognize:Windows, Linux, or OSX                  *
*******************************************************
SET:CopyPath=Part
SET:SyslogS:127.0.0.1
SET:SyslogP=5514
SET:SyslogL=min
****************************************************************
* Windows: Is Plaso in the AChoir directory? If not, Install   *
****************************************************************
VER:Windows
  SAY:[+] Windows Timelining...
  SAY:[+] Checking for Plaso in: &Dir\Plaso
  CKN:&Dir\Plaso\log2timeline.exe
    DIR:\Plaso
    SAY:
    SAY:[*] Log2TimeLine Not Found, Downloading it... 
    SAY:
    GET:https://github.com/OMENScan/AChoir/blob/master/Tools/plaso-20191203-py3.7-amd64.zip?raw=true &Dir\Plaso.zip
    SAY:
    SAY:[*] Unzipping Plaso...
    SAY:
    UNZ:&Dir\Plaso.zip &Dir
    SAY:
  END:
  DIR:\
  REQ:&Dir\Plaso\log2timeline.exe
  REQ:&Dir\Plaso\psort.exe
  SAY:
  SAY:[+] Plaso (log2timeline, psort) has been found in: &Dir\Plaso\
  *******************************************************
  * Get the Collection To Timeline                      *
  *******************************************************
  ACQ:\
  CKN:&ACQ\Triage
    SAY:
    SAY:[!] That Collection Does Not Exist. Exiting.
    BYE:
  END:
  SAY:[+] Now TimeLining &Dir\&Inp
  *******************************************************
  * Timeline MFT(s)                                     *
  *******************************************************
  ACQ:\TimeLine
  ACQ:\
  SAY:[+] Now TimeLining &ACQ\Triage\C\$MFT
  SET:SyslogL=none
  EXE:&Dir\Plaso\Log2TimeLine.exe -z UTC --parsers "mft" --status_view none --quiet --logfile "&Acq\Temp.Log" &Acq\Timeline\timeline.bodyfile "&ACQ\Triage\C\$MFT"
  *******************************************************
  * Timeline Event ViewerLogs                           *
  *******************************************************
  SET:SyslogL=min
  SAY:[+] Now TimeLining &ACQ\Triage\C\Windows\System32\winevt\Logs\**\*.evtx
  SET:SyslogL=none
  FOR:&ACQ\Triage\C\Windows\System32\winevt\Logs\**\*.evtx
  EXE:&Dir\Plaso\Log2TimeLine.exe -z UTC --parsers "winevt,winevtx" --status_view none --quiet --logfile "&Acq\Temp.Log" &Acq\Timeline\timeline.bodyfile "&For"
  *******************************************************
  * Timeline Prefetch                                   *
  *******************************************************
  SET:SyslogL=min
  SAY:[+] Now TimeLining &ACQ\Triage\C\Windows\Prefetch\*.*
  SET:SyslogL=none
  FOR:&ACQ\Triage\C\Windows\Prefetch\*.*
  EXE:&Dir\Plaso\Log2TimeLine.exe -z UTC --parsers "prefetch" --status_view none --quiet --logfile "&Acq\Temp.Log" &Acq\Timeline\timeline.bodyfile "&For"
  *******************************************************
  * Timeline System Registries                          *
  *******************************************************
  SET:SyslogL=min
  SAY:[+] Now TimeLining &ACQ\Triage\C\Windows\System32\config\*.*
  SET:SyslogL=none
  FOR:&ACQ\Triage\C\Windows\System32\config\**\*
  EXE:&Dir\Plaso\Log2TimeLine.exe -z UTC --parsers "winreg_default,winreg" --status_view none --quiet --logfile "&Acq\Temp.Log" &Acq\TimeLine\timeline.bodyfile "&For"
  *******************************************************
  * Timeline User Registries                            *
  *******************************************************
  SET:SyslogL=min
  SAY:[+] Now TimeLining &ACQ\Triage\C\Users\*\[Nn][Tt][Uu][Ss][Ee][Rr].[Dd][Aa][Tt]
  SET:SyslogL=none
  FOR:&ACQ\Triage\C\Users\*\[Nn][Tt][Uu][Ss][Ee][Rr].[Dd][Aa][Tt]
  EXE:&Dir\Plaso\Log2TimeLine.exe -z UTC --parsers "winreg_default,winreg" --status_view none --quiet --logfile "&Acq\Temp.Log" &Acq\Timeline\timeline.bodyfile "&For"
  *******************************************************
  * Convert BodyFile to CSV                             *
  *******************************************************
  SET:SyslogL=min
  SAY:[+] Coverting BodyFile to CSV
  SET:SyslogL=none
  EXE:&Dir\Plaso\psort.exe --temporary_directory &Acq\Timeline -z UTC -o l2tcsv -w &Acq\Timeline\timeline.csv &Acq\Timeline\timeline.bodyfile
END:
****************************************************************
* All Processing Complete                                      *
****************************************************************
SET:SyslogL=min
SAY:[+] BodyFile Coverted to CSV for: &Acq
SAY:[+] Plaso Processing Complete for: &Acq
