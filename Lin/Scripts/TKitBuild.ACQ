**********************************************************
** ToolKit Builder & Embedder (v0.05)                    *
** /VR0:<Windows, Limux, MacOS)                          *
** /VR1:<Source Directory>                               *
** /VR2:<Name of the Collector>                          *
**********************************************************
SAY:[+] Welcome to AChoirX Builder version 0.05
SAY:[+] Platform: &VR0
SAY:[+] Source Directory: &VR1
SAY:[+] Collector Name: &VR2
**********************************************************
** Validate that we got all the Parameters we need       *
**********************************************************
CN0:0
EQU:&VR0. .
 SAY:[!] The Platform (Windows, Linux, OSX) was not Specified (/VR0:) 
 CN0:++
END:
EQU:&VR1. .
 SAY:[!] The Source Directory Was Not Specified (/VR1:)
 CN0:++
END:
EQU:&VR2. .
 SAY:[!] The Collector Name Was Not Specified (/VR2:)
 CN0:++
END:
N>>:&CN0 0
 SAY:[!] Not Enough Information for Toolkit Builder:
 SAY:[!] Toolkit Builder requires /VR0:Platform /VR1:Source_Directory /VR2:New_Collector_Name
 SAY:[!] Toolkit Builder - Not Enough Information... Exiting...
 SYS:cmd /c copy /Y &Dir\Logs\&ACN.log &Dir\Logs\TKitBuild.log
 JMP:TKitDone
END:
VER:Windows
 DEL:Logs\TKitBuild.log
 ACQ:\&VR2
 SET:CopyPath=Part
 CPY:"&VR1\**\*" "&ACQ"
 SET:ZipFileName=&DIR\&ACN\&VR2.zip
 FOR:&ACQ\**\*
 ZIP:&For
 SET:ZipFileName=close
 EXE:&Dir\A-AChoirX.exe /PKR:&DIR\&ACN\&VR2.zip
 SYS:cmd /c move &Dir\A-AChoirX_PKR.exe &DIR\&ACN\&VR2.exe
 SYS:cmd /c copy /Y &Dir\Logs\&ACN.log &Dir\Logs\TKitBuild.log
 SAY:[+] Build Complete...
END: 
Ver:Linux
 ACQ:\&VR2
 SET:CopyPath=Part
 CPY:"&VR1/**/*" "&ACQ"
 SET:ZipFileName=&DIR/&ACN/&VR2.zip
 FOR:&ACQ/**/*
 ZIP:&For
 SET:ZipFileName=close
 SYS:&Dir/AChoirX /PKR:&DIR/&ACN/&VR2.zip
 SYS:mv &Dir/AChoirX_PKR &DIR/&ACN/&VR2_exe
 SAY:[+] Build Complete...
END: 
Ver:OSX
 ACQ:\&VR2
 SET:CopyPath=Part
 CPY:"&VR1/**/*" "&ACQ"
 SET:ZipFileName=&DIR/&ACN/&VR2.zip
 FOR:&ACQ/**/*
 ZIP:&For
 SET:ZipFileName=close
 EQU:&PRC arm64
  SYS:&Dir/AChoirX-ARM /PKR:&DIR/&ACN/&VR2.zip
  SYS:mv &Dir/AChoirX-ARM_PKR &DIR/&ACN/&VR2_exe
 END:
 EQU:&PRC amd64
  SYS:&Dir/AChoirX-AMD /PKR:&DIR/&ACN/&VR2.zip
  SYS:mv &Dir/AChoirX-AMD_PKR &DIR/&ACN/&VR2_exe
 END:
 SAY:[+] Build Complete...
END:
LBL:TKitDone
PZZ:Press any key to Exit
